# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Taijian <taijian@posteo.de>
# Contributor: Sebastian Lau <lauseb644@gmail.com>
# Contributor: Damian01w <damian01w@gmail.com>
# Contributor: Padfoot <padfoot@exemail.com.au>

pkgname=plymouth
pkgver=24.004.60
pkgrel=14
pkgdesc='Graphical boot splash screen'
arch=(x86_64)
url='https://www.freedesktop.org/wiki/Software/Plymouth/'
license=(GPL-2.0-or-later)
depends=(
  adwaita-fonts
  bash
  cairo
  filesystem
  fontconfig
  freetype2
  glib2
  glibc
  libdrm
  libevdev
  libpng
  libx11
  libxkbcommon
  pango
  systemd-libs
  xkeyboard-config
)
makedepends=(
  docbook-xsl
  git
  gtk3
  meson
)
optdepends=('gtk3: x11 renderer')
backup=(
  etc/logrotate.d/bootlog
  etc/plymouth/plymouthd.conf
)
install='plymouth.install'
source=(
  "git+https://gitlab.freedesktop.org/plymouth/$pkgname.git#tag=$pkgver"
  plymouth.initcpio_hook
  plymouth.initcpio_install
  plymouth-shutdown.initcpio_install
  50-plymouth.conf
  archcraft-logo.png
  plymouthd.conf
)
b2sums=('a3d55f4f7be81bdf2ddd5c2b74a3fdb4e368c31fc41e12ab100ce2a7986cb418151b3df0d0316011710dd0e1ae99631166eecf80bc1dd5cc9054a4685266afed'
        'afb2449b542aa3e971eab6b953c907347fdf4e499b4140a5e6736a7c99557c0d8d2fed28dbee56d84c8c619335c59bd382457d85e51145884ad0616e9095f232'
        'e9efbfd924a07005e883c4cd21d25c520288daf8bf611ee20a245b8b8f925bc90188dfcbb9e13da883479c5214562795717bb4404dd7ef1444eb04ab69dad4a9'
        '063448411de837ed38ece935719f07fd17b18830680c9fa95b7bd39a097778186c40373590504c9b44144125986304311f528c73592c29d19043b8395e6f99c2'
        '5ab47942d222c311b495d056f24bdd9d08438162fef1ca6e51982fc00018acbf0c31e4c61164b40950fe2511e17ba2a4635894d145d83a9b349e5a8a83f8c026'
        '6084349acab7f39e31192b945131f856fafc775e51ad7297a803ac2ce9452e66e8794de450be9f350113b1853fdfe68fb49ed8238e25a170dc427d7573eadf79'
        '3967a5b7eb1f10f54733eceef64c7da1f0230c29fa8dc1b195d4d7b0efbea581bed5d4c1d84a36de790f820a0c761329f7021c250cb32aad547ff707e914a380')

prepare() {
  cd $pkgname

  # Various fixes from upstream
  git cherry-pick -n -m 1 24.004.60..e88c403bda64d301b0434045513551c1ccdee983

  # Use mkinitcpio to update initrd
  sed -i 's/^dracut -f$/mkinitcpio -P/' scripts/plymouth-update-initrd

  # Change default theme
  sed -i 's/^Theme=spinner$/Theme=archcraft/' src/plymouthd.defaults

  # Switch from Cantarell to Adwaita Sans; set monospace font to Adwaita Mono
  sed -i 's/Cantarell/Adwaita Sans/
          /TitleFont=Adwaita/a MonospaceFont=Adwaita Mono 12' scripts/keymap-render.py themes/*/*.plymouth.desktop
}

build() {
  arch-meson $pkgname build \
    -D logo=/usr/share/pixmaps/archcraft-logo.png
  meson compile -C build

  # Convert logo for the spinner theme
  #rsvg-convert '/usr/share/pixmaps/archcraft-logo.svg' -o archcraft-logo.png
}

package() {
  meson install -C build --destdir "$pkgdir"
  rm -r "$pkgdir/run"

  # Install mkinitcpio hook
  install -Dm644 plymouth.initcpio_hook "$pkgdir/usr/lib/initcpio/hooks/$pkgname"
  install -Dm644 plymouth.initcpio_install "$pkgdir/usr/lib/initcpio/install/$pkgname"

  # Install mkinitcpio shutdown hook and systemd drop-in snippet
  install -Dm644 plymouth-shutdown.initcpio_install "$pkgdir/usr/lib/initcpio/install/$pkgname-shutdown"
  install -Dm644 50-plymouth.conf "$pkgdir/usr/lib/tmpfiles.d/50-plymouth.conf"
  
  # Install logo for the spinner theme
  install -Dm644 archcraft-logo.png "$pkgdir/usr/share/$pkgname/themes/spinner/watermark.png"

  # Install plymouthd.conf config file
  install -Dm644 plymouthd.conf "$pkgdir/etc/$pkgname/plymouthd.conf"

  # Fix archlinux-logo.png issue
  sed -i -e 's/archlinux-logo.png/archcraft-logo.png/g' "$pkgdir/usr/lib/initcpio/install/$pkgname"
}
